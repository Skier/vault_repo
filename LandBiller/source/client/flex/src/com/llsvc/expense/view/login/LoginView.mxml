<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="vertical" width="300" title="Login">

	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.managers.PopUpManager;
			import mx.controls.Alert;
			import com.llsvc.domain.events.UserEvent;
			import com.llsvc.domain.vo.userVO;
			import com.llsvc.domain.User;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.events.ValidationResultEvent;
			import com.llsvc.services.UserService;
			import mx.rpc.Responder;
			
			[Bindable] private var isBusy:Boolean = false;
			[Bindable] private var user:User;
			[Bindable] private var usStates:ArrayCollection;
			
            public static function open(parent:DisplayObject, user:User = null, modal:Boolean = true):LoginView
            {
                var popup:LoginView = new LoginView();
                
                popup.user = user;
                if (user != null) 
                {
                	popup.callLater(popup.login, [user.login.username, user.login.password]);
                }

                PopUpManager.addPopUp(popup, parent, modal);
                PopUpManager.centerPopUp(popup);
                
                return popup;
            }

            public function close():void
            {
                PopUpManager.removePopUp(this);
            }
            
			private function isFormValid():Boolean 
			{
				var result:Boolean = true;
				
				if (vUsername.validate().type == ValidationResultEvent.INVALID) 
					result = false;
				
				if (vPassword.validate().type == ValidationResultEvent.INVALID) 
					result = false;

				return result;
			}
			
			private function startRemoting():void 
			{
				isBusy = true;
			}
			
			private function stopRemoting():void 
			{
				isBusy = false;
			}
			
			private function login(username:String = null, password:String = null):void 
			{
				if (username == null)
					username = txtUsername.text;
				
				if (password == null)
					password = txtPassword.text;
				
				startRemoting();
				UserService.instance.login(txtUsername.text, txtPassword.text).addResponder(
					new mx.rpc.Responder(
						function (event:ResultEvent):void 
						{
							stopRemoting();
							var result:userVO = (event.result as Array).pop() as userVO;
							if (result == null)
							{
								Alert.show("User or Password is incorrect!", "Login failed");
								return;
							}
							var user:User = new User();
							user.userid = result.userid;
							
							dispatchEvent(new UserEvent(UserEvent.LOGIN_SUCCESSFULL, user)); 
						},
						function (event:FaultEvent):void 
						{
							stopRemoting();
							Alert.show("Login failed. Username or Password is incorrect.", "Login failed");
						}
					));
			}
			
			private function btnLoginClickHandler(event:*):void 
			{
				if (!isFormValid())
					return;
					
				login(txtUsername.text, txtPassword.text);
			}
			
			private function btnRegisterClickHandler(event:*):void 
			{
				dispatchEvent(new Event("registrationRequest"));
			}
			
		]]>
	</mx:Script>

	<mx:StringValidator id="vUsername" source="{txtUsername}" property="text" maxLength="50"/>
	<mx:StringValidator id="vPassword" source="{txtPassword}" property="text" maxLength="50"/>

	<mx:Form width="100%">
		<mx:FormItem label="Username" width="100%">
			<mx:TextInput id="txtUsername" width="100%"
				text="{user.login.username}" 
				enabled="{!isBusy}"
				enter="btnLoginClickHandler(null)"/>
		</mx:FormItem>
		<mx:FormItem label="Password" width="100%">
			<mx:TextInput id="txtPassword" displayAsPassword="true" width="100%" 
				text="{user.login.password}" 
				enabled="{!isBusy}"
				enter="btnLoginClickHandler(null)"/>
		</mx:FormItem>
	</mx:Form>

	<mx:ProgressBar labelPlacement="right" label="" indeterminate="true" width="100%"
		visible="{isBusy}" includeInLayout="{isBusy}"/>

	<mx:ControlBar horizontalAlign="center" width="100%">
		<mx:Button height="24" label="Registration"
			enabled="{!isBusy}"
			click="btnRegisterClickHandler(event)"/>
		<mx:Spacer width="100%" height="100%"/>
		<mx:Button height="24" label="Login"
			enabled="{!isBusy}" 
			click="btnLoginClickHandler(event)"/>
	</mx:ControlBar>
</mx:TitleWindow>
