<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:controls="com.llsvc.controls.*">
	
	<mx:Script>
		<![CDATA[
			import com.llsvc.domain.DocumentProject;
			import mx.managers.PopUpManager;
			import com.llsvc.domain.Document;
			import com.llsvc.domain.Project;
			import com.llsvc.client.lms.LocalCash;
			import mx.collections.ArrayCollection;
			import mx.controls.ComboBox;
			
			private var _docum:Document;
			[Bindable]
			public function get docum():Document { return _docum; }
			public function set docum(value:Document):void 
			{
				_docum = value;
				
				if (_docum != null) 
				{
					init(_docum);
					_docum.addEventListener("projectCollectionChange", projectCollectionChangeHandler);
				}
			}
			
			[Bindable]
			private var projectList:ArrayCollection;
			
            public static function open(parent:DisplayObject, docum:Document, modal:Boolean):ProjectSelector
            {
                var popup:ProjectSelector = new ProjectSelector();
                popup.docum = docum;
                
                popup.init(docum);
                
                PopUpManager.addPopUp(popup, parent, modal);
                
                return popup;
            }

            public function close():void
            {
                PopUpManager.removePopUp(this);
            }
            
            public function init(doc:Document):void 
            {
            	var projects:ArrayCollection = LocalCash.getInstance().getProjects();
            	
            	projectList = new ArrayCollection();
            	for each (var proj:Project in projects) 
            	{
            		projectList.addItem(new ProjectWrapper(proj));
            	}
            	
            	updateSelections();
            }
            
            private function updateSelections():void 
            {
            	for each (var pw:ProjectWrapper in projectList) 
            	{
            		if (isProjectExists(pw.project, docum.projects)) 
            		{
            			pw.isSelected = true;
            		} else {
            			pw.isSelected = false;
            		}
            	}
            }
            
            private function isProjectExists(project:Project, collection:ArrayCollection):Boolean 
            {
            	for each (var dp:DocumentProject in collection) 
            	{
            		if (dp.project.id == project.id) 
            		{
            			return true;
            		}
            	}
            	
            	return false;
            }
            
            private function projectCollectionChangeHandler(event:Event):void 
            {
            	updateSelections();
            }
            
            private function cbProjectChangeHandler(event:Event):void 
            {
            	docum.projects.removeAll();
            	for (var i:int = 0; i < projectList.length; i++) 
            	{
	            	var cb:mx.controls.CheckBox = cbProject[i] as mx.controls.CheckBox;
	            	var projectWrapper:ProjectWrapper = projectList.getItemAt(i) as ProjectWrapper;

            		if (cb.selected) 
            		{
            			var dp:DocumentProject = new DocumentProject();
            			dp.document = docum;
            			dp.project = projectWrapper.project;
            			
            			docum.projects.addItem(dp);
            		}
            	}
            }
            
		]]>
	</mx:Script>
	
	<mx:VBox id="boxProjects"
		verticalGap="0"
		width="100%" height="100%"
		backgroundColor="#ffffee">
		<controls:PercentBoxFixed direction="vertical" 
			width="100%" height="100%" 
    		paddingLeft="4" paddingRight="4" paddingTop="4" paddingBottom="4"
			verticalGap="0">
			<mx:Repeater id="rptProjects" dataProvider="{projectList}">
	            <mx:CheckBox id="cbProject" 
	            	selected="{ProjectWrapper(rptProjects.currentItem).isSelected}"
	            	enabled="{ProjectWrapper(rptProjects.currentItem).project.isActive}"
	            	label="{ProjectWrapper(rptProjects.currentItem).name}" 
	            	change="cbProjectChangeHandler(event)" verticalCenter="0"/>
			</mx:Repeater>
		</controls:PercentBoxFixed>
	</mx:VBox>
</mx:Canvas>
