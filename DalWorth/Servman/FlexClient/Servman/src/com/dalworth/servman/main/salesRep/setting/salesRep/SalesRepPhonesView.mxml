<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml">
	
	<mx:Script>
		<![CDATA[
			import com.dalworth.servman.main.bp.PhoneEditPopup;
			import com.dalworth.servman.events.PhoneEvent;
			import mx.core.Application;
			import com.dalworth.servman.domain.Phone;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import com.dalworth.servman.service.PhoneService;
			import mx.collections.ArrayCollection;
			import com.dalworth.servman.domain.SalesRep;
			import mx.rpc.Responder;
			
			[Bindable]
			private var ownPhones:ArrayCollection = new ArrayCollection();
			[Bindable]
			private var companyPhones:ArrayCollection = new ArrayCollection();
			
			private var _salesRep:SalesRep;
			[Bindable]
			public function get salesRep():SalesRep { return _salesRep; }
			public function set salesRep(value:SalesRep):void 
			{
				_salesRep = value;
				initPhones();
			}
			
			public function getOwnPhones():Array
			{
				return ownPhones.source;
			}
			
			public function getCompanyPhones():Array
			{
				return companyPhones.source;
			}
			
			private function initPhones():void 
			{
				ownPhones.removeAll();
				companyPhones.removeAll();
				
				if (salesRep == null)
					return;
					
				PhoneService.getInstance().getPhonesBySalesRep(salesRep).addResponder(
					new mx.rpc.Responder(
						function (event:ResultEvent):void 
						{
							var result:Array = event.result as Array;
							var len:int = result.length;
							var phone:Phone;
							for (var i:int = 0; i < len; i++)
							{
								phone = result[i] as Phone;
								if (phone.isCompanyPhone)
									companyPhones.addItem(phone);
								else 
									ownPhones.addItem(phone);
							}
						},
						function (event:FaultEvent):void 
						{
							trace(event.fault.message);
						}
					));
			}
			
			private function editPhone(phone:Phone):void 
			{
				var popup:PhoneEditPopup = PhoneEditPopup.open(Application.application as DisplayObject, phone);
				popup.addEventListener(PhoneEvent.PHONE_COMMIT, 
					function (event:PhoneEvent):void 
					{
						var phone:Phone = popup.phone;
						if (!ownPhones.contains(phone))
							ownPhones.addItem(phone);
						var idx:int = ownPhones.getItemIndex(phone);
						dgOwnPhones.selectedIndex = idx;
						dgOwnPhones.scrollToIndex(idx); 
					});
			}
			
			private function onAddPhone():void 
			{
				var phone:Phone = new Phone();
				editPhone(phone);
			}
			
			private function onDoubleClickPhone():void 
			{
				var phone:Phone = dgOwnPhones.selectedItem as Phone;
				editPhone(phone);
			}
			
			private function onRemovePhone():void 
			{
				var phone:Phone = dgOwnPhones.selectedItem as Phone;
				var idx:int = ownPhones.getItemIndex(phone);
				ownPhones.removeItemAt(idx);
			}
			
		]]>
	</mx:Script>
	
	<mx:HBox width="100%" height="100%">
		<mx:VBox width="100%" height="100%">
			<mx:Label text="Own Phone Numbers:"/>
			<mx:DataGrid id="dgOwnPhones" width="100%" height="100%" dataProvider="{ownPhones}"
				sortableColumns="false" draggableColumns="false" editable="false"
				doubleClickEnabled="true" itemDoubleClick="onDoubleClickPhone()">
				<mx:columns>
					<mx:DataGridColumn headerText="Phone Number" dataField="Number"/>
					<mx:DataGridColumn headerText="Description" dataField="Description"/>
				</mx:columns>
			</mx:DataGrid>
			
			<mx:HBox width="100%">
				<mx:Button label="Remove" click="onRemovePhone()" enabled="{dgOwnPhones.selectedItem != null}" width="80"/>
				<mx:Spacer width="100%"/>
				<mx:Button label="Add" click="onAddPhone()" width="80"/>
			</mx:HBox>
		</mx:VBox>
		
		<mx:VBox width="100%" height="100%">
			<mx:Label text="Assotiated Company Phones::"/>
			<mx:DataGrid id="dgCompanyPhones" width="100%" height="100%" dataProvider="{companyPhones}"
				sortableColumns="false" draggableColumns="false" editable="false">
				<mx:columns>
					<mx:DataGridColumn headerText="Phone Number" dataField="Number"/>
					<mx:DataGridColumn headerText="Description" dataField="Description"/>
				</mx:columns>
			</mx:DataGrid>
		</mx:VBox>
	</mx:HBox>
	
</mx:Canvas>
