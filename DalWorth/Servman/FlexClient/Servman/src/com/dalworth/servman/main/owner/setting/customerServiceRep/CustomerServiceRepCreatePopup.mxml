<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical"
	width="600"  
    title="Create CSR" close="btnCloseClickHandler(event)" xmlns:lead="com.dalworth.servman.main.lead.*" xmlns:controls="com.affilia.controls.*"
    xmlns:containers="com.arc90.flexlib.containers.*" xmlns:voice="com.affilia.voice.*" xmlns:user="com.dalworth.servman.main.customerServiceRep.setting.user.*" xmlns:user1="com.dalworth.servman.main.owner.setting.user.*">
    
    <mx:Metadata>
        [Event(name="leadSave", type="com.dalworth.servman.events.LeadEvent")]
    </mx:Metadata>

    <mx:Script>
        <![CDATA[
        	import com.dalworth.servman.events.CustomerServiceRepEvent;
        	import com.dalworth.servman.service.CustomerServiceRepService;
        	import com.dalworth.servman.domain.CustomerServiceRep;
        	import com.dalworth.servman.service.BusinessPartnerService;
        	import mx.events.ValidationResultEvent;
        	import com.dalworth.servman.events.BusinessPartnerEvent;
        	import com.dalworth.servman.domain.SalesRep;
        	import com.dalworth.servman.domain.User;
        	import com.dalworth.servman.domain.BusinessPartner;
        	import com.dalworth.servman.main.MainAppModel;
        	import com.dalworth.servman.domain.LeadStatus;
        	import mx.controls.Alert;
        	import mx.rpc.events.FaultEvent;
        	import com.dalworth.servman.events.LeadEvent;
        	import mx.rpc.events.ResultEvent;
        	import com.dalworth.servman.service.LeadService;
        	import mx.core.UIComponent;
        	import mx.collections.ArrayCollection;
        	import mx.events.CloseEvent;
        	import com.dalworth.servman.domain.Lead;
            import mx.managers.PopUpManager;
            import mx.rpc.Responder;
            
            [Bindable] private var isBusy:Boolean = false;
            [Bindable] private var isReadOnly:Boolean = false;
            
			private var _currentCustomerServiceRep:CustomerServiceRep;
			[Bindable]
			public function get currentCustomerServiceRep():CustomerServiceRep { return _currentCustomerServiceRep; }
			public function set currentCustomerServiceRep(value:CustomerServiceRep):void 
			{
				_currentCustomerServiceRep = value;
			}
			
            public static function open(parent:DisplayObject, currentCustomerServiceRep:CustomerServiceRep, isReadOnly:Boolean = false, modal:Boolean = true):CustomerServiceRepCreatePopup
            {
                var popup:CustomerServiceRepCreatePopup = new CustomerServiceRepCreatePopup();
                
                popup.currentCustomerServiceRep = currentCustomerServiceRep;
                popup.isReadOnly = isReadOnly;

                PopUpManager.addPopUp(popup, parent, modal);
                PopUpManager.centerPopUp(popup);
                
                return popup;
            }

            public function close():void
            {
                PopUpManager.removePopUp(this);
            }
            
            private function btnCloseClickHandler(event:CloseEvent):void 
            {
                if (!isBusy)
                    close();
            }
            
			private function isFormValid():Boolean
			{
				var result:Boolean = true;
				
				vName.enabled = true;
				if (vName.validate().type == ValidationResultEvent.INVALID)
					result = false;
				
				if (!viewUserInfo.isValid())
					result = false;

				return result;
			}

			private function onSaveClick():void 
			{
				if (!isFormValid())
					return;
				
				var newCustomerServiceRep:CustomerServiceRep = currentCustomerServiceRep.prepareToSend();
				
				newCustomerServiceRep.ShowAs = txtName.text;
				newCustomerServiceRep.IsActive = cbIsActive.selected;
				
				newCustomerServiceRep.RelatedUser = viewUserInfo.getUser();
				newCustomerServiceRep.RelatedUser.Name = txtName.text;
				
				var view:UIComponent = this;
				view.enabled = false;
				CustomerServiceRepService.getInstance().saveCustomerServiceRep(newCustomerServiceRep).addResponder(
					new mx.rpc.Responder(
						function (event:ResultEvent):void 
						{
							view.enabled = true;
							var result:CustomerServiceRep = event.result as CustomerServiceRep
							currentCustomerServiceRep.applyFields(result);
							currentCustomerServiceRep.RelatedUser = result.RelatedUser;
							dispatchEvent(new CustomerServiceRepEvent(CustomerServiceRepEvent.CUSTOMER_SERVICE_REP_SAVE, currentCustomerServiceRep));
							
							close();
						},
						function (event:FaultEvent):void 
						{
							view.enabled = true;
							Alert.show(event.fault.message);
						}));
			}

			private function onCancelClick():void 
			{
				close();
			}
			
        ]]>
    </mx:Script>

	<mx:StringValidator id="vName" source="{txtName}" property="text" required="{currentCustomerServiceRep.Id == 0}" enabled="false"/>
	
	<mx:VBox width="100%" height="100%" paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10"
		borderColor="#999999" borderStyle="solid" fontSize="12">
		<mx:Form width="100%" minWidth="250" labelWidth="120"
			paddingLeft="0" paddingRight="0" paddingTop="0" paddingBottom="0">
			<mx:FormItem label="CSR Name:" width="100%" required="true">
				<mx:TextInput id="txtName" width="100%" text="{currentCustomerServiceRep.RelatedUser.Name}"/>
				<mx:CheckBox id="cbIsActive" label="is active" selected="{currentCustomerServiceRep.IsActive}"/>
			</mx:FormItem>
		</mx:Form>

		<user1:UserEditView id="viewUserInfo" width="100%" user="{currentCustomerServiceRep.RelatedUser}"/>
	</mx:VBox>
	
    <mx:ControlBar horizontalAlign="center" width="100%"  enabled="{!isBusy}">
		<mx:Spacer width="100%"/>
		<mx:Button label="Create" click="onSaveClick()"/>
        <mx:LinkButton label="Cancel" click="onCancelClick()"/>
    </mx:ControlBar>
</mx:TitleWindow>
