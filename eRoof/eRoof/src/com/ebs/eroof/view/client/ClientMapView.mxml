<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:maps="com.ebs.eroof.view.maps.*" xmlns:mapping="com.ebs.eroof.mapping.*">
	
	<mx:Script>
		<![CDATA[
			import com.afcomponents.umap.types.LatLng;
			import com.quickbase.idn.control.events.QuickBaseEventStoreRecord;
			import com.quickbase.idn.control.events.KingussieEventCallBack;
			import com.ebs.eroof.dto.Clients_DTO;
			import com.ebs.eroof.model.wrapper.Facility;
			import com.ebs.eroof.model.wrapper.Client;
			
			private var mapIsReady:Boolean = false;
			
			private var _model:Client;
			[Bindable]
			public function get model():Client { return _model; }
			public function set model(value:Client):void 
			{
				_model = value;
				refreshContent();
			}
			
			private function refreshContent():void 
			{
				if (!model || !mapIsReady)
					return;
				
				mapContainer.clearMap();

				for each (var facility:Facility in model.facilityCollection)
				{
					mapContainer.addMarker(facility.getMarker());
				}
				
				setMapView();
			}
			
			private function setMapView():void 
			{
				mapContainer.setMapCenter(model.getPosition());
				mapContainer.setMapZoom(model.getZoom());
			}
			
			private function mapIsReadyHandler(event:Event):void 
			{
				mapIsReady = true;
				refreshContent();
			}
			
			public function getCurrentPosition():LatLng
			{
				return mapContainer.getMapCenter();
			}
			
			public function getCurrentZoom():Number
			{
				return mapContainer.getMapZoom();
			}
			
			private function updateModel():void 
			{
				model.setPosition(getCurrentPosition());
				model.setZoom(getCurrentZoom());
			}

			private function saveClientClickHandler():void 
			{
				updateModel();
				saveClient();
			}

            private function saveClient():void 
            {
            	var callBackEvent:KingussieEventCallBack = new KingussieEventCallBack(onSaveModel, model.clientDTO);
            	var ev:QuickBaseEventStoreRecord = new QuickBaseEventStoreRecord(model.clientDTO, callBackEvent);
            	ev.dispatch();
            }
            
            private function onSaveModel(storedObject:Clients_DTO):void 
            {
            	model.clientDTO = storedObject;
            }
            
		]]>
	</mx:Script>


	<mapping:MapContainer id="mapContainer" left="0" right="0" top="0" bottom="24"
		mapIsReady="mapIsReadyHandler(event)"/>
	<mx:HBox width="100%" bottom="0" horizontalAlign="right">
		<mx:Button label="Save Map" click="saveClientClickHandler()"/>
	</mx:HBox>
</mx:Canvas>
