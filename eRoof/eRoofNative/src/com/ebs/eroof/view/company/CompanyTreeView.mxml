<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml">
	
    <mx:Metadata>
        [Event(name="changeSelectedObject")]
    </mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.ebs.eroof.model.wrapper.Facility;
			import com.ebs.eroof.model.wrapper.Segment;
			import com.ebs.eroof.model.wrapper.Client;
			import com.ebs.eroof.model.wrapper.Section;
			import com.ebs.eroof.model.company.CompanyTreeDataDescriptor;
			import mx.core.UIComponent;
			import com.ebs.eroof.control.CompanyTreeController;
			import com.ebs.eroof.model.company.CompanyTreeModel;
			import com.ebs.eroof.model.company.Company;
			
			[Bindable]
			private var model:CompanyTreeModel = CompanyTreeModel.getInstance();
			private var controller:CompanyTreeController = new CompanyTreeController(this as UIComponent);
			
            [Embed(source="assets/icons16/book_brown.png")]
            [Bindable]
            private var iconCompany:Class;

            [Embed(source="assets/icons16/book.png")]
            [Bindable]
            private var iconSegment:Class;

            [Embed(source="assets/icons16/user_business.png")]
            [Bindable]
            private var iconClient:Class;

            [Embed(source="assets/icons16/home.png")]
            [Bindable]
            private var iconFacility:Class;

            [Embed(source="assets/icons16/layout_select_sidebar.png")]
            [Bindable]
            private var iconSection:Class;

			public function init():void 
			{
				loadCompanyInfo();
			}
			
			private function loadCompanyInfo():void 
			{
				dispatchEvent(new Event(CompanyTreeController.INIT_COMPANY_TREE));
			}
			
			public function getSelectedObject():Object 
			{
				return treeCompany.selectedItem;
			}
			
			public function selectObject(obj:Object):void 
			{
				if (obj is Section)
					expandSection(obj as Section);
				else if (obj is Facility)
					expandFacility(obj as Facility);
				else if (obj is Client)
					expandClient(obj as Client);
				else if (obj is Segment)
					expandSegment(obj as Segment);
				else if (obj is Company)
					expandCompany(obj as Company);
				
				treeCompany.selectedItem = obj;
				treeCompany.scrollToIndex(treeCompany.getItemIndex(obj));
			}
			
			private function expandCompany(company:Company):void 
			{
				treeCompany.expandItem(company, true);
			}
			
			private function expandSegment(segment:Segment):void 
			{
				expandCompany(segment.company);
				treeCompany.expandItem(segment, true);
			}
			
			private function expandClient(client:Client):void 
			{
				expandSegment(client.segment);
				treeCompany.expandItem(client, true);
			}
			
			private function expandFacility(facility:Facility):void 
			{
				expandClient(facility.client);
				treeCompany.expandItem(facility, true);
			}
			
			private function expandSection(section:Section):void 
			{
				expandFacility(section.facility);
				treeCompany.expandItem(section, true);
			}
			
			private function treeCompanyItemClickHandler():void 
			{
                var item:Object = treeCompany.selectedItem;
                
                if (item == null)
                	return;
                else 
                	treeCompany.expandItem(item, true, true);
			}
			
			private function treeCompanyChangeHandler():void 
			{
				dispatchEvent(new Event("changeSelectedObject"));
			}
			
            private function getIcon(item:Object):Class 
            {
                if (item is Company) 
                	return iconCompany;
                else if (item is Segment) 
                	return iconSegment;
                else if (item is Client) 
                	return iconClient;
                else if (item is Facility) 
                	return iconFacility;
                else if (item is Section) 
                	return iconSection;
                else  
                	return null;
            }
            
			
		]]>
	</mx:Script>
	
	<mx:VBox width="100%" height="100%"
		paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5"
		backgroundColor="#eeeeee"
		borderColor="#999999" borderStyle="solid" cornerRadius="2">
		<mx:Label text="Segments:" fontSize="16" fontWeight="bold" color="#AA0000" width="100%" textAlign="left"/>
		<mx:Tree id="treeCompany"
			width="100%" height="100%" borderColor="#666666" borderStyle="solid"
			labelField="name" 
			dataDescriptor="{new CompanyTreeDataDescriptor()}" 
			dataProvider="{model.company}"
			itemClick="treeCompanyItemClickHandler()"
			change="treeCompanyChangeHandler()"
			iconFunction="getIcon"/>
	</mx:VBox>
</mx:Canvas>
