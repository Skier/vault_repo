<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" creationComplete="onCreationComplete()"
	title="{'Not Exceed Rate info. Asset:' + rate.RelatedAssetAssignment.RelatedAsset.Name + ', AFE:' + rate.RelatedAssetAssignment.AFE + ', SubAFE: ' + rate.RelatedAssetAssignment.SubAFE}"
	>

	<mx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			import mx.rpc.events.FaultEvent;
			import common.TypesRegistry;

			[Bindable]
			private var _types:ArrayCollection = new ArrayCollection();
			
			private var _collection:ArrayCollection;

			[Bindable]
			public var rate:RateByAssignment;
			
            public static function Open(rate:RateByAssignment, collection:ArrayCollection, parent:DisplayObject, modal:Boolean = true):NotExceedDetail 
            {
                var popup:NotExceedDetail = new NotExceedDetail();
                popup.rate = rate;
                popup._collection = collection;

                PopUpManager.addPopUp(popup, parent, modal);
                PopUpManager.centerPopUp(popup);

                return popup;
            }
            
            private function onCreationComplete():void 
            {
            	initTypes();

            	cbBillItemType.selectedIndex = _types.getItemIndex(getTypeById(rate.BillItemTypeId));
            }
            
            private function initTypes():void 
            {
            	var types:ArrayCollection = TypesRegistry.instance.getAllBillItemTypes;
            	_types.removeAll();
            	
            	for each (var type:BillItemType in types) {
            		if (typeIsAvailable(type) && !type.IsPresetRate) {
            			_types.addItem(type);
            		}
            	}
            }
            
			private function typeIsAvailable(type:BillItemType):Boolean 
			{
				if (rate.BillItemTypeId == type.BillItemTypeId) {
					return true;
				}

				for each (var r:RateByAssignment in _collection) {
					if (r.BillItemTypeId == type.BillItemTypeId) {
						return false;
					}
				}

				return true;
			}

            private function getTypeById(id:int):BillItemType 
            {
            	for each (var type:BillItemType in _types) {
            		if (type.BillItemTypeId == id) {
            			return type;
            		}
            	}
            	return null;
            }
            
            private function onBillItemTypeChange():void {
            	var defaultRateCollection:ActiveCollection = ActiveRecords.DefaultBillRate.findByBillItemTypeIdAndAssetId(
            		BillItemType(cbBillItemType.selectedItem).BillItemTypeId, rate.RelatedAssetAssignment.AssetId);
            	if (defaultRateCollection.IsLoaded) {
            		if (1 == defaultRateCollection.length) {
            			txtBillRate.text = DefaultBillRate(defaultRateCollection[0]).BillRate.toFixed(3);
            		}
            	} else {
            		defaultRateCollection.addEventListener("loaded", onDefaultRateLoaded);
            	}
            }
            
            private function onDefaultRateLoaded(evt:DynamicLoadEvent):void {
            	var defaultRateCollection:ActiveCollection = ActiveCollection(evt.data);
            	defaultRateCollection.removeEventListener("loaded", onDefaultRateLoaded);
            	if (1 == defaultRateCollection.length) {
	            	txtBillRate.text = DefaultBillRate(defaultRateCollection[0]).BillRate.toFixed(3);
	            }
            }
            
		]]>
	</mx:Script>
	
	<mx:VBox width="100%">
		<mx:HBox>
			<mx:Label text="Bill Item Type:" width="130" textAlign="right"/>
			<mx:ComboBox
				id="cbBillItemType"
				width="120"
				labelField="Name"
				dataProvider="{_types}"
				change="onBillItemTypeChange()"
				prompt=""/>
		</mx:HBox>
		<mx:HBox>
			<mx:Label text="Not Exceed Rate:" width="130" textAlign="right"/>
			<mx:TextInput id="txtBillRate" text="{rate.BillRate.toFixed(3)}" width="150" maxChars="15"/>
		</mx:HBox>
	</mx:VBox>
	
	<mx:HRule width="100%"/>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Button id="btnSubmit" label="Ok"
			enabled="{cbBillItemType.selectedItem != null &amp;&amp; !isNaN(Number(txtBillRate.text))}"/>
		<mx:Button id="btnCancel" label="Cancel"/>
	</mx:HBox>
	
</mx:TitleWindow>
