<?xml version="1.0" encoding="utf-8"?>
<mx:Box xmlns:mx="http://www.adobe.com/2006/mxml">

	<mx:Metadata>
		[Event(name="fileCreateDrawing", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileCreateTract", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileOpenDrawing", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileOpenTract", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileSave", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileAttachDrawing", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileExportExcel", type="src.appMenu.AppMenuEvent")]
		[Event(name="filePrint", type="src.appMenu.AppMenuEvent")]
		[Event(name="fileLogout", type="src.appMenu.AppMenuEvent")]
		[Event(name="viewZoomAll", type="src.appMenu.AppMenuEvent")]
		[Event(name="viewZoomIn", type="src.appMenu.AppMenuEvent")]
		[Event(name="viewZoomOut", type="src.appMenu.AppMenuEvent")]
		[Event(name="settingsShowArea", type="src.appMenu.AppMenuEvent")]
		[Event(name="settingsShowAnnotation", type="src.appMenu.AppMenuEvent")]
		[Event(name="settingsMore", type="src.appMenu.AppMenuEvent")]
    </mx:Metadata>
    
    <mx:Script>
        <![CDATA[
            import src.deedplotter.domain.Document;
            import src.deedplotter.domain.settings.UserSettingsRegistry;
            import mx.binding.utils.BindingUtils;
            import src.deedplotter.domain.Tract;
            import mx.events.MenuEvent;
            
            [Bindable]
            public var model:AppMenuModel = new AppMenuModel();
            
            private var _currentTract:Tract;
            public function get currentTract():Tract { return _currentTract; }
            public function set currentTract(value:Tract):void
            {
                _currentTract = value;

                var currentTractNotNull:Boolean = (null != currentTract);
                
                getMenuItem(AppMenuEvent.FILE_EXPORT_EXCEL).enabled = currentTractNotNull;
                getMenuItem(AppMenuEvent.FILE_EXPORT_PDF).enabled = currentTractNotNull;
                getMenuItem(AppMenuEvent.FILE_PRINT).enabled = currentTractNotNull;
                getMenuItem(AppMenuEvent.VIEW_ZOOM_ALL).enabled = currentTractNotNull;
                getMenuItem(AppMenuEvent.VIEW_ZOOM_IN).enabled = currentTractNotNull;
                getMenuItem(AppMenuEvent.VIEW_ZOOM_OUT).enabled = currentTractNotNull;

                getMenuItem(AppMenuEvent.FILE_ATTACH_DRAWING).enabled = 
                    currentTractNotNull && currentTract.ParentDocument == null;

                getMenuItem(AppMenuEvent.VIEW_ZOOM_OUT).enabled = 
                    currentTractNotNull && currentTract.IsDirty;
                
                model.data.refresh();

                if (currentTractNotNull)
                {
                    BindingUtils.bindSetter(currentTract_IsDirtyHandler, currentTract, "IsDirty");
                    
                    if (currentTract.DocId == 0)
                        BindingUtils.bindSetter(currentTract_DocHandler, currentTract, "ParentDocument");
                }
            }
            
            public function reset():void
            {
                model = new AppMenuModel();

                getMenuItem(AppMenuEvent.SETTINGS_SHOW_ANNOTATION).toggled = 
                    UserSettingsRegistry.getInstance().ShowAnnotations;

                getMenuItem(AppMenuEvent.SETTINGS_SHOW_AREA).toggled = 
                    UserSettingsRegistry.getInstance().ShowArea;
            }
            
            public function getMenuItem(eventType:String):Object
            {
                return model.getMenuItemByEvent(model.data.source, eventType);
            }

            private function itemClickHandler(event:MenuEvent):void
            {
                var item:Object = event.item;
                
                dispatchEvent(new AppMenuEvent(item.eventType, item));
            }
            
            private function currentTract_IsDirtyHandler(value:Boolean):void
            {
                getMenuItem(AppMenuEvent.FILE_SAVE).enabled = value;
    
                model.data.refresh();
            }

            private function currentTract_DocHandler(value:Document):void
            {
                getMenuItem(AppMenuEvent.FILE_ATTACH_DRAWING).enabled = (null == value);
    
                model.data.refresh();
            }

        ]]>
    </mx:Script>
    
    <mx:MenuBar id="mainMenu"
        dataProvider="{model.data}"
        width="100%" labelField="label"
        itemClick="itemClickHandler(event)"/>

</mx:Box>
