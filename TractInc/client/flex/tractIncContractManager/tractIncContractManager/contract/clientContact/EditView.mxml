<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow 
    xmlns:mx="http://www.adobe.com/2006/mxml"
    layout="vertical" title="Client Contact" 
    xmlns:validators="TractInc.SDK.validators.*"
    xmlns:components="TractInc.SDK.components.*"
    creationComplete="init();">

    <mx:Script>
        <![CDATA[
            import mx.collections.ArrayCollection;
            
            import mx.rpc.Responder;
            import mx.rpc.events.FaultEvent;
            import mx.rpc.events.ResultEvent;
            
            import mx.events.ItemClickEvent;
            import mx.controls.CheckBox;
            import mx.controls.Label;
            import mx.controls.Alert;
            import mx.managers.PopUpManager;

            import TractInc.SDK.utils.DateHelper;
            import TractInc.Domain.Person;
            import TractInc.Domain.Contract;
            import TractInc.Domain.ClientContact;
            import TractInc.Domain.ContractStatus;
            import TractInc.Domain.InvoiceItemType;
            import TractInc.Domain.Company;
            import TractInc.Domain.Client;
            import tractIncContractManager.ContractManagerController;
            
            [Bindable]
            private var canEdit:Boolean = false;
            [Bindable]
            private var controller:ClientContactController = null;
            private var currentClientContact:ClientContact = null;
                
            public static function open(ctrl:ClientContactController, clientContact:ClientContact, modal:Boolean):EditView
            {
                var popup:EditView = new EditView();
                popup.postCreate(ctrl, clientContact);
                
                PopUpManager.addPopUp(popup, ctrl.view as DisplayObject, modal);
                PopUpManager.centerPopUp(popup);
                
                return popup;
            }

            public function postCreate(ctrl:ClientContactController, clientContact:ClientContact):void
            {
                controller = ctrl;
                currentClientContact = clientContact;
            }
            
            public function init():void
            {
                canEdit = true; //controller.model.userManagerPackage.canManageUsers;
                
                var persons:Array = [null];
                for each (var a:Person in controller.contractPackage.ContractClient.PersonList) {
                    persons.push(a);
                }
                this.cbPersonList.dataProvider = persons;
                this.cbPersonList.labelField = "LastName";
                
                if ( null != currentClientContact ) {
                    populatePersonForm(currentClientContact.ContactPerson);
                    
                    this.dtStartDate.value = currentClientContact.StartDate;
                    if ( !DateHelper.isNullDate(currentClientContact.EndDate) ) {
                        this.dtEndDate.value = currentClientContact.EndDate;
                    }
                    
                    this.cbPersonList.selectedItem = 
                            controller.getClientPersonById(
                                    currentClientContact.PersonId);
                } else {
                    currentClientContact = new ClientContact();
                    currentClientContact.ContactPerson = new Person();
                }
            }
            
            private function populatePersonForm(person:Person):void
            {
                if ( null != person ) {
                    this.txtFirstName.text = person.FirstName;
                    this.txtLastName.text = person.LastName;
                    this.txtMiddleName.text = person.MiddleName;
                    this.txtEmail.text = person.Email;
                    this.txtPhone.text = person.PhoneNumber;
                    currentClientContact.ContactPerson = person;
                } else {
                    this.txtFirstName.text = "";
                    this.txtLastName.text = "";
                    this.txtMiddleName.text = "";
                    this.txtEmail.text = "";
                    this.txtPhone.text = "";
                    currentClientContact.ContactPerson = new Person();
                }
            }
            
            public function getClientContact():ClientContact
            {
                currentClientContact.ContactPerson.FirstName = this.txtFirstName.text;
                currentClientContact.ContactPerson.LastName = this.txtLastName.text;
                currentClientContact.ContactPerson.MiddleName = this.txtMiddleName.text;
                currentClientContact.ContactPerson.Email = this.txtEmail.text;
                currentClientContact.ContactPerson.PhoneNumber = this.txtPhone.text;
                
                currentClientContact.StartDate = this.dtStartDate.value;
                currentClientContact.EndDate = this.dtEndDate.value;
                
                return currentClientContact;
            }

            public function close():void
            {
                PopUpManager.removePopUp(this);
            }
            
            private function commit():void
            {
                if ( !formValidator.validate(true) ) {
                    return;
                }
                controller.saveClientContact(getClientContact());
            }

            public function availablePersonChangeHandler():void 
            {
                populatePersonForm(this.cbPersonList.selectedItem as Person);
            }
        ]]>
    </mx:Script>

    <validators:ValidatorsGroup id="formValidator">
        <mx:StringValidator source="{txtFirstName}" property="text" required="true"/>
        <mx:StringValidator source="{txtLastName}" property="text" required="true"/>
        <mx:StringValidator source="{txtEmail}" property="text" required="true"/>
        <mx:StringValidator source="{txtPhone}" property="text" required="true"/>
        <mx:StringValidator source="{dtStartDate}" property="value" required="true"/>
    </validators:ValidatorsGroup>

    <mx:Form id="mainForm" enabled="{canEdit}" 
        backgroundColor="#ffffff" borderStyle="solid" borderColor="#000000"
        paddingBottom="20" paddingLeft="20" paddingRight="20" paddingTop="20">

        <mx:FormItem label="Available Persons" width="100%" required="true">
            <mx:ComboBox id="cbPersonList" change="availablePersonChangeHandler()" width="100%"/>
        </mx:FormItem>
        
        <mx:FormHeading label="Contact Person Information"/>
        <mx:FormItem label="First Name:">
            <mx:TextInput id="txtFirstName" maxChars="50"/>
        </mx:FormItem>
        <mx:FormItem label="Last Name:">
            <mx:TextInput id="txtLastName" maxChars="50"/>
        </mx:FormItem>
        <mx:FormItem label="Middle Name:">
            <mx:TextInput id="txtMiddleName" maxChars="50"/>
        </mx:FormItem>
        <mx:FormItem label="Email:">
            <mx:TextInput id="txtEmail" maxChars="50"/>
        </mx:FormItem>
        <mx:FormItem label="Phone Num:">
            <mx:TextInput id="txtPhone" maxChars="50"/>
        </mx:FormItem>
            
        <mx:FormHeading label="Dates"/>
        <mx:HBox>
            <mx:FormItem label="Start Date" width="100%" required="true">
                <components:CombinedDateField id="dtStartDate" width="100%"/>
            </mx:FormItem>
            <mx:FormItem label="End Date" width="100%">
                <components:CombinedDateField id="dtEndDate" width="100%"/>
            </mx:FormItem>
        </mx:HBox>
    </mx:Form>
            
    <mx:ControlBar horizontalAlign="right">
        <mx:Button label="Save" click="commit()" enabled="{canEdit}"/>
        <mx:Button label="Cancel" click="close()"/>
    </mx:ControlBar>
    
</mx:TitleWindow>
